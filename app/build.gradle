buildscript {
    repositories {
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    }
    dependencies {
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.4'
        classpath 'org.robolectric:robolectric-gradle-plugin:0.14.1'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'android-apt'
apply plugin: 'robolectric'

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.1"

    defaultConfig {
        applicationId 'com.athome.zubaliy.mylifeontheroad'

        minSdkVersion 18
        targetSdkVersion 21
        versionCode 1
        versionName '0.0.1'

        def SimpleDateFormat = "yyyy-MM-dd\'T\'HH:mm:ss.SSS\'Z\'"

        /* Default MongoDB and JavaScript ISO date format */
        buildConfigField "java.text.SimpleDateFormat", "SIMPLEDATEFORMAT", "new java.text.SimpleDateFormat(\"$SimpleDateFormat\")"
        buildConfigField "String", "DATEFORMAT", "\"$SimpleDateFormat\""

        /* Keys */
        buildConfigField "String", "KEY_DEVICE_MAC_ADDRESS", "\"device_mac_address\""
        buildConfigField "String", "KEY_CONFIG_LINK_SERVER_LOCAL", "\"server_local\""
        buildConfigField "String", "KEY_CONFIG_LINK_SERVER_WEB", "\"server_web\""
        buildConfigField "String", "KEY_SHORT_JOURNEY", "\"short_journey\""
        buildConfigField "String", "KEY_SHORT_BREAK", "\"short_break\""

        /* Upload URLs */
        buildConfigField "String", "LINK_UPLOAD_ACTIVITYLOGS_SERVER", "\"https://lor-aroadz.rhcloud.com/lifeonroad/api/insert/ActivityLogs\""
        buildConfigField "String", "LINK_UPLOAD_ACTIVITYLOGS_LOCAL", "\"http://192.168.1.4:8080/open/lifeonroad/api/insert/ActivityLogs\""
    }
    buildTypes {
        debug {
            zipAlignEnabled false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        release {
            zipAlignEnabled true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    //exlude duplicate files from apache commons
    packagingOptions {
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'LICENSE.txt'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

configurations {
    apt
}

apt {
    arguments {
        resourcePackageName android.defaultConfig.applicationId
        androidManifestFile variant.outputs[0].processResources.manifestFile
    }
}



def AndroidAnnotationsVersion = '3.2'
def OrmLiteVersion = '4.48'
def LombokVersion = '1.14.8'
def RobolectricVersion = '2.4'
def JUnitVersion = '4.12'


dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:appcompat-v7:21.0.3'


    apt "org.androidannotations:androidannotations:$AndroidAnnotationsVersion"
    compile "org.androidannotations:androidannotations-api:$AndroidAnnotationsVersion"

    compile "com.j256.ormlite:ormlite-android:$OrmLiteVersion"
    compile "com.j256.ormlite:ormlite-core:$OrmLiteVersion"

    apt "org.projectlombok:lombok:$LombokVersion"
    provided "org.projectlombok:lombok:$LombokVersion"

    compile 'org.apache.commons:commons-lang3:3.3.2'
    compile 'com.fasterxml.jackson.core:jackson-core:2.4.3'
    compile 'com.google.code.gson:gson:2.3'

    androidTestCompile("junit:junit:$JUnitVersion")
    androidTestCompile("org.robolectric:robolectric:$RobolectricVersion") {
        exclude module: 'classworlds'
        exclude module: 'commons-logging'
        exclude module: 'httpclient'
        exclude module: 'maven-artifact'
        exclude module: 'maven-artifact-manager'
        exclude module: 'maven-error-diagnostics'
        exclude module: 'maven-model'
        exclude module: 'maven-project'
        exclude module: 'maven-settings'
        exclude module: 'plexus-container-default'
        exclude module: 'plexus-interpolation'
        exclude module: 'plexus-utils'
        exclude module: 'support-v4'
        exclude module: 'wagon-file'
        exclude module: 'wagon-http-lightweight'
        exclude module: 'wagon-provider-api'
    }

}