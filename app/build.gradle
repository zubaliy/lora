buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.4'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'android-apt'

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.1"

    defaultConfig {
        applicationId 'com.athome.zubaliy.mylifeontheroad'

        minSdkVersion 18
        targetSdkVersion 21
        versionCode 1
        versionName '0.0.1'

        /* Default MongoDB and JavaScript ISO date format */
        buildConfigField "java.text.SimpleDateFormat", "SDF", "new java.text.SimpleDateFormat(\"yyyy-MM-dd'T'HH:mm:ss.SSS'Z'\")"

        /* Keys */
        buildConfigField "String", "KEY_DEVICE_MAC_ADDRESS", "\"device_mac_address\""
        buildConfigField "String", "KEY_CONFIG_LINK_SERVER_LOCAL", "\"server_local\""
        buildConfigField "String", "KEY_CONFIG_LINK_SERVER_WEB", "\"server_web\""
        buildConfigField "String", "KEY_SHORT_JOURNEY", "\"short_journey\""
        buildConfigField "String", "KEY_SHORT_BREAK", "\"short_break\""

        /* Upload URLs */
        buildConfigField "String", "LINK_UPLOAD_ACTIVITYLOGS_SERVER", "\"https://lor-aroadz.rhcloud.com/lifeonroad/api/insert/ActivityLogs\""
        buildConfigField "String", "LINK_UPLOAD_ACTIVITYLOGS_LOCAL", "\"http://192.168.1.4:8080/open/lifeonroad/api/insert/ActivityLogs\""
    }
    buildTypes {
        debug {
            zipAlignEnabled false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        release {
            zipAlignEnabled true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    //exlude duplicate file from apache commons
    packagingOptions {
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

configurations {
    apt
}

apt {
    arguments {
        resourcePackageName android.defaultConfig.applicationId
        androidManifestFile variant.outputs[0].processResources.manifestFile
    }
}

def AndroidAnnotationsVersion = '3.2'
def OrmLiteVersion = '4.48'
def LombokVersion = '1.14.8'

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:appcompat-v7:21.0.3'

    apt "org.androidannotations:androidannotations:$AndroidAnnotationsVersion"
    compile "org.androidannotations:androidannotations-api:$AndroidAnnotationsVersion"

    compile "com.j256.ormlite:ormlite-android:$OrmLiteVersion"
    compile "com.j256.ormlite:ormlite-core:$OrmLiteVersion"

    apt "org.projectlombok:lombok:$LombokVersion"
    compile "org.projectlombok:lombok:$LombokVersion"

    compile 'org.apache.commons:commons-lang3:3.3.2'
    compile 'org.springframework.android:spring-android-rest-template:2.0.0.M1'
    compile 'com.fasterxml.jackson.core:jackson-core:2.4.3'
    compile 'com.google.code.gson:gson:2.3'
}